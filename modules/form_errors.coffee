mod =

  identifier: "formErrors"
  name: 'Form Error Display'
  description: "Displays Form Errors generated by the Rails framework in a nice, bootstrappy way."

  config:
    feedbackClasses:
      error: "glyphicon glyphicon-remove form-control-feedback"

  init: (main) ->
    main.registerCallback 'ajaxComplete', 'Form errors after AJAX calls', mod.displayErrors
    main.registerCallback 'documentReady', 'Form errors after a document load', mod.displayErrors

  displayErrors: (context) ->
    jQuery("[data-error]").each () ->
      elem = jQuery(@)
      help_tag = jQuery("<span></span>")
      help_tag.addClass('help-block')
      help_tag.text(elem.data('error'))

      form_group = elem.parents('.form-group').first()
      form_group.addClass('has-error has-feedback')

      unless elem.data('noFeedback')?
        feedback_tag = jQuery("<span></span>")
        feedback_tag.addClass autoJS.config.formErrors.feedbackClasses.error
        feedback_tag.addClass 'no-label' if elem.data('noLabel')
        feedback_tag.insertAfter(elem)

      form_group.append(help_tag)

autoJS.registerModule mod


